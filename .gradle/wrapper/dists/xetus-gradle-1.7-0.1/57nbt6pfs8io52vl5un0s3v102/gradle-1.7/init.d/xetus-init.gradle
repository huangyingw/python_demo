/* 
 * Standard initialization for startup scripts
 */
apply plugin:XetusGradle

class XetusGradle implements Plugin<Gradle> {
  void apply(Gradle gradle) {
    
    /*
     * Expose the xetusGradle extension to all projects
     */
    gradle.allprojects {
      project -> project.extensions.create("xetusGradle", XetusGradleExtensions, project)
    }
    
    /* 
     * Make sure every project applies the corp plugin, even if manually
     */
    gradle.allprojects {
      project ->
        project.afterEvaluate {
          if (!getPlugins().hasPlugin("corp")){
            throw new InvalidUserCodeException("All projects must apply the corp plugin."
                                               +" Use xetusGradle.useCorpPlugin(version)")
          }
        }
    }
  }
}

class XetusGradleExtensions {
  private final Project project
  
  XetusGradleExtensions(Project project){
    this.project = project
  }
  
  void useCorpPlugin(String version){
    def xetusArchiva = "https://archiva.xetus.com/archiva/repository/xetus-repository/"
    project.buildscript {
      repositories {
        mavenLocal()
        maven { url xetusArchiva }
      }
      dependencies {
        classpath "com.xetus.build.gradle:corpplugin:$version"
      }
    }
  }
}